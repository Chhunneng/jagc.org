#!/usr/bin/perl -l

use warnings;

use IPC::Run qw/run timeout/;
use JSON::PP 'encode_json';

my $interpret = shift // '/usr/bin/perl';
my $dir       = shift // '/opt/share/';
my $timeout   = shift // 10;
my $code      = shift // '/opt/share/code';
my $input     = shift // './input';
my $arguments = shift // '';

binmode STDOUT, ':utf8';
binmode STDERR, ':utf8';

chdir $dir;

open my $in, '<', $input or do {
  print encode_json {status => 'fail', stderr => "Can't open input file: $!"};
  exit;
};

my @cmd;
if (length $arguments > 0) {
  @cmd = ($interpret, $arguments, $code);
} else {
  @cmd = ($interpret, $code);
}

my ($out, $err) = (undef, undef);
my $res = eval { run \@cmd, $in, \$out, \$err, timeout($timeout); };
close $in;

my $status = $@ ? 'fail' : $res ? 'ok' : 'error';
my $result = {status => $status, stderr => $status eq 'fail' ? $@ : $err, stdout => $out};
print encode_json $result;
